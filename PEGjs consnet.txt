{
	var localLobule = options.lobuleName;
	var localLobe = options.lobeName;
}


document
= form*


form
= pair
/ structure
/ json_value
/ prism_chain
/ lobeLobuleId
/ lobuleId
/ id


pair
= '[' _* left:form right:form _* ']' _* {
	return {
    	type: "pair",
    	left: left,
        right: right
    };
}


structure
= head:word '(' _* c:structure_content* _* ')' _* {
	return {
    	type: "structure",
    	head: head,
        content: c
    };
}


json_value
= '{' j:$naked_object '}' {
	return {
    	type: "value",
        value: JSON.parse('{'+j+'}')
    };
}


_
= [ \t\r\n]


prism_chain
= w:($(_* '<' word '>' _*))+ _* {
	return {
    	type: "prism",
        chain: w.map(prism => prism.replace(/[<>]/g,'').trim())
    };
}


lobeLobuleId
= lo:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ '.' lu:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ '.' id:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ _* {
	return lo.join('')+'.'+lu.join('')+'.'+id.join('');
}


lobuleId
= lu:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ '.' id:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ _* {
	return localLobe+'.'+lu.join('')+'.'+id.join('');
}


id
= id:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ _* {
	return localLobe+'.'+localLobule+'.'+id.join('');
}


word
= id:[^ \t\r\n\[\]\(\)\{\}\<\>\.]+ {
	return id.join('');
}


structure_content
= arg:form _* { return arg; }


naked_object
  = members:(
      head:member
      tail:(value_separator m:member { return m; })*
      {
        var result = {};

        [head].concat(tail).forEach(function(element) {
          result[element.name] = element.value;
        });

        return result;
      }
    )?
    { return members !== null ? members : {}; }


JSON_text
  = ws value:value ws { return value; }

begin_array     = ws "[" ws
begin_object    = ws "{" ws
end_array       = ws "]" ws
end_object      = ws "}" ws
name_separator  = ws ":" ws
value_separator = ws "," ws

ws "whitespace" = [ \t\n\r]*

// ----- 3. Values -----

value
  = false
  / null
  / true
  / object
  / array
  / number
  / string

false = "false" { return false; }
null  = "null"  { return null;  }
true  = "true"  { return true;  }

// ----- 4. Objects -----

object
  = begin_object
    members:(
      head:member
      tail:(value_separator m:member { return m; })*
      {
        var result = {};

        [head].concat(tail).forEach(function(element) {
          result[element.name] = element.value;
        });

        return result;
      }
    )?
    end_object
    { return members !== null ? members: {}; }

member
  = name:string name_separator value:value {
      return { name: name, value: value };
    }

// ----- 5. Arrays -----

array
  = begin_array
    values:(
      head:value
      tail:(value_separator v:value { return v; })*
      { return [head].concat(tail); }
    )?
    end_array
    { return values !== null ? values : []; }

// ----- 6. Numbers -----

number "number"
  = minus? int frac? exp? { return parseFloat(text()); }

decimal_point
  = "."

digit1_9
  = [1-9]

e
  = [eE]

exp
  = e (minus / plus)? DIGIT+

frac
  = decimal_point DIGIT+

int
  = zero / (digit1_9 DIGIT*)

minus
  = "-"

plus
  = "+"

zero
  = "0"

// ----- 7. Strings -----

string "string"
  = quotation_mark chars:char* quotation_mark { return chars.join(""); }

char
  = unescaped
  / escape
    sequence:(
        '"'
      / "\\"
      / "/"
      / "b" { return "\b"; }
      / "f" { return "\f"; }
      / "n" { return "\n"; }
      / "r" { return "\r"; }
      / "t" { return "\t"; }
      / "u" digits:$(HEXDIG HEXDIG HEXDIG HEXDIG) {
          return String.fromCharCode(parseInt(digits, 16));
        }
    )
    { return sequence; }

escape
  = "\\"

quotation_mark
  = '"'

unescaped
  = [^\0-\x1F\x22\x5C]

// ----- Core ABNF Rules -----

// See RFC 4234, Appendix B (http://tools.ietf.org/html/rfc4234).
DIGIT  = [0-9]
HEXDIG = [0-9a-f]i